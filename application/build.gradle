buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:$springCloudContractDependenciesVersion"
    }
}


apply plugin: GitRepositoryUrlPlugin

subprojects {

    apply plugin: "java-library"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenCentral()
        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }

    dependencies {
        implementation(platform("io.eventuate.platform:eventuate-platform-dependencies:$eventuatePlatformVersion"))

        implementation(platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))
        implementation(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
        implementation(platform("org.springframework.cloud:spring-cloud-contract-dependencies:$springCloudContractDependenciesVersion"))

        testImplementation "junit:junit:4.13.2"

        constraints {
            implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"
        }

        testImplementation "org.springframework.boot:spring-boot-starter-test"

    }

    tasks.withType(Copy).all { duplicatesStrategy 'WARN' }

    if (System.getenv("GRADLE_CACHE_CHANGING_VERSIONS_IN_MINUTES") != null)
        configurations.all {
            resolutionStrategy.cacheChangingModulesFor Integer.parseInt(System.getenv("GRADLE_CACHE_CHANGING_VERSIONS_IN_MINUTES")), 'minutes'
        }

}

task clean(type:Delete) {
    delete 'build/repo'
}

task endToEndTests(type: GradleBuild) {
    startParameter.setBuildCacheEnabled(false)
    tasks = [":end-to-end-tests:endToEndTest"]
    dependsOn(":end-to-end-tests:clean")
}


task endToEndTestsUsingKind(type: GradleBuild) {
    startParameter.projectProperties.put("endToEndTestMode", "Kind")
    startParameter.setBuildCacheEnabled(false)
    tasks = [":end-to-end-tests:endToEndTest"]
    dependsOn(":end-to-end-tests:clean")
}


task compileAll(type: GradleBuild) {
    tasks = ["endToEndTestClasses", "componentTestClasses", "integrationTestClasses", "testClasses", "assemble"]
}

task testEachService(type: GradleBuild) {
    tasks = ["test", "integrationTest", "contractTest", "componentTest"]
}

